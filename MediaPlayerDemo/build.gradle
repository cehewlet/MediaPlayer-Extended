apply plugin: 'com.android.application'

android {
    compileSdk 33

    buildFeatures {
        buildConfig = true
    }

    defaultConfig {
        // Keep old ITEC package name as application Id for Play Store compatibility
        applicationId 'at.aau.itec.android.mediaplayerdemo'

        minSdk 16
        targetSdk 33
        versionCode 1
        versionName '1.0'
    }

    signingConfigs {
        debug   // configured in signingconfig.gradle
        release // configured in signingconfig.gradle
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    namespace 'net.protyposis.android.mediaplayerdemo'

    lint {
        // Lint fix for Okio: https://github.com/square/okio/issues/58
        warning 'InvalidPackage'
    }

    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            if (variant.name == android.buildTypes.release.name) {
                output.outputFileName = output.outputFileName.replace(".apk", "-" + defaultConfig.versionCode + "-" + defaultConfig.versionName + ".apk")
            }
        }
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation project(':MediaPlayer')
    implementation project(':MediaPlayer-DASH')
    implementation platform('com.google.firebase:firebase-bom:32.7.2')
    implementation 'com.google.firebase:firebase-crashlytics'
}

ext.isLibrary = false
apply from: "../gitversioning.gradle"
apply from: "signingconfig.gradle"

def firebaseConfigFile = file("google-services.json");
if (!firebaseConfigFile.exists()) {
    project.logger.error("Firebase config file for Crashlytics not found at ${firebaseConfigFile.absolutePath}. Please download the file from the Firebase Console and put it there.")
}

apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'
