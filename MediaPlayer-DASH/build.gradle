apply plugin: 'com.android.library'
apply plugin: 'maven-publish'
apply plugin: 'signing'

android {
    compileSdk 33

    buildFeatures {
        buildConfig = true
    }

    defaultConfig {
        minSdk 16
        targetSdk 33
        buildConfigField "String", "VERSION_NAME", "\"${libraryVersionString()}\""
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    namespace 'net.protyposis.android.mediaplayer.dash'

    lint {
        // Lint fix for Okio: https://github.com/square/okio/issues/58
        warning 'InvalidPackage'
    }

    publishing {
        singleVariant("release") {
            withSourcesJar()
            withJavadocJar()
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation project(':MediaPlayer')
    implementation 'com.squareup.okio:okio:1.8.0'
    implementation "com.squareup.okhttp3:okhttp:3.4.2"
    implementation "com.googlecode.mp4parser:isoparser:1.0.5.4"
}

publishing {
    publications {
        release(MavenPublication) {
            artifactId = 'mediaplayer-dash'
            pom {
                description = 'MediaPlayer-Extended DASH extension module'
            }
            afterEvaluate {
                from components.release
            }
        }
    }
}

signing {
    useGpgCmd()
    sign publishing.publications
}
