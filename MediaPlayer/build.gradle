apply plugin: 'com.android.library'
apply plugin: 'maven-publish'
apply plugin: 'signing'

android {
    compileSdk 33

    buildFeatures {
        buildConfig = true
    }

    defaultConfig {
        minSdk 16
        targetSdk 33
        buildConfigField "String", "VERSION_NAME", "\"${libraryVersionString()}\""
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    namespace 'net.protyposis.android.mediaplayer'
    publishing {
        singleVariant("release") {
            withSourcesJar()
            withJavadocJar()
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    testImplementation 'junit:junit:4.12'
}

publishing {
    publications {
        release(MavenPublication) {
            artifactId = 'mediaplayer'
            pom {
                description = 'MediaPlayer-Extended core module'
            }
            afterEvaluate {
                from components.release
            }
        }
    }
}

signing {
    useGpgCmd()
    sign publishing.publications
}